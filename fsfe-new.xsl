<?xml version="1.0" encoding="ISO-8859-1"?>

<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <xsl:output method="html"
           encoding="ISO-8859-1"
           doctype-public="-//W3C//DTD HTML 4.0 Transitional//EN"
           doctype-system="http://www.w3.org/TR/REC-html40/loose.dtd"
           indent="yes"
           />

  <xsl:template match="buildinfo">
    <xsl:apply-templates select="node()" />
  </xsl:template>

  <xsl:template match="buildinfo/document">
    <html>
      <xsl:apply-templates select="node()" />
    </html>
  </xsl:template>

  <xsl:template match="body">
    <body>
      <xsl:comment>

        DO NOT MODIFY THIS DOCUMENT. IT WAS GENERATED BY XSLT PROCESSING
        AND YOUR MODIFICATIONS WILL BE LOST. THE SOURCE OF THE DOCUMENT
        IS IN THE .xml FILE USE make all TO REGENERATE

      </xsl:comment>
      <table width="100%">
        <tr>
          <td>
            <a href="/"><img src="/graphics/global/logo.png" border="0" /></a>
          </td>
          <td class="focus">
            <table class="focus">
              <tr>
                <td class="focus-top-hinge">
                   <xsl:value-of select="/buildinfo/textset/text[@id='focus']" />
                </td>
                <td class="focus-box">
                   <form action="/cgi-bin/nph-redirect.cgi" method="get">
                     <select name="address" onchange="window.location.href=this.options[this.selectedIndex].value">
                       <option value="#"> - <xsl:value-of select="/buildinfo/textset/text[@id='select']" /> -</option>
                       <option><xsl:attribute name="value">http://www.new.fsfeurope.org<xsl:value-of select="/buildinfo/@filename" />.<xsl:value-of select="/buildinfo/@language" />.html</xsl:attribute>Global</option>
                       <option><xsl:attribute name="value">http://france.new.fsfeurope.org<xsl:value-of select="/buildinfo/@filename" />.<xsl:value-of select="/buildinfo/@language" />.html</xsl:attribute>France</option>

<option><xsl:attribute name="value">http://germany.new.fsfeurope.org<xsl:value-of select="/buildinfo/@filename" />.<xsl:value-of select="/buildinfo/@language" />.html</xsl:attribute>Germany</option>
                     </select>
                   </form>
                 </td>
              </tr>
              <!-- <tr>
                <td class="focus-top-hinge">
                   <xsl:value-of select="/buildinfo/textset/text[@id='find']" />
                </td>
                <td class="focus-box">
                  <form method="post" action="/search.cgi">
                    <input type="text" size="10" name="var" />
                  </form>
                </td>
              </tr> -->
            </table>
          </td>
        </tr>
        <tr>
          <td class="line" colspan="2">
            <div align="center">
              <xsl:for-each select="/buildinfo/trlist/tr">
                <xsl:sort select="@id" />
                  <xsl:choose>
                    <xsl:when test="@id=/buildinfo/@language">
                      <xsl:value-of select="." />
                    </xsl:when>
                    <xsl:otherwise>
                      <xsl:element name="a">
                        <xsl:attribute name="href"><xsl:value-of select="/buildinfo/@filename" />.<xsl:value-of select="@id" />.html</xsl:attribute>
                        <xsl:value-of select="." disable-output-escaping="yes" />
                      </xsl:element>
                    </xsl:otherwise>
                  </xsl:choose>
                <xsl:text> </xsl:text>
              </xsl:for-each>
              <xsl:if test="/buildinfo/@outdated='yes'"><br />
                <xsl:apply-templates select="/buildinfo/textset/text[@id='outdated']" />
              </xsl:if>
              <xsl:if test="/buildinfo/@language!=/buildinfo/document/@language"><br />
                <xsl:apply-templates select="/buildinfo/textset/text[@id='notranslation']" />
              </xsl:if>
            </div>
          </td>
        </tr>
        <tr>
          <td class="body" valign="top">
            <xsl:apply-templates select="node()"/>
          </td>
          <td class="menu" width="150">
            <xsl:for-each select="/buildinfo/menuset/menu[@parent='']">
              <xsl:sort select="@id" />
              <xsl:variable name="id"><xsl:value-of select="@id" /></xsl:variable>
              <a class="menu" href="{.}">
                <xsl:value-of select="/buildinfo/textset/text[@id=$id]" />
              </a> <br />
              <xsl:for-each select="/buildinfo/menuset/menu[@parent=$id]">
                <xsl:sort select="@id" />
                <a class="submenu" href="{.}">
                  <xsl:variable name="id"><xsl:value-of select="@id" /></xsl:variable>
                  <xsl:value-of select="/buildinfo/textset/text[@id=$id]" />
                </a> <br />
              </xsl:for-each>
            </xsl:for-each>
          </td>
        </tr>
        <tr>
          <td class="line" colspan="2">
            <div align="center">
              <xsl:apply-templates select="/buildinfo/textset/text[@id='copyright']/node()" />
            </div>
          </td>
        </tr>
      </table>
    </body>
  </xsl:template>

  <xsl:template match="head">
    <head>
      <link rel="stylesheet" type="text/css" href="/blue-new.css" />
      <xsl:apply-templates select="@*|node()"/>
    </head>
  </xsl:template>

  <xsl:template match="timestamp|buildinfo/set|buildinfo/textset|buildinfo/menuset|buildinfo/trlist" />

  <xsl:template match="@*|node()" priority="-1">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
    </xsl:copy>
  </xsl:template>

</xsl:stylesheet>
